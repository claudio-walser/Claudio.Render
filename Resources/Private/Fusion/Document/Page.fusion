/**
 * Default page-rendering for the Claudio.Render website
 */
prototype(Claudio.Render:Document.Page) < prototype(Neos.Fusion:Component) {
    mainMenu = Claudio.Render:Document.Fragment.Menu.Main
    secondaryMenu = Claudio.Render:Document.Fragment.Menu.Secondary
    metaMenu = Claudio.Render:Document.Fragment.Menu.Meta
    breadcrumbMenu = Claudio.Render:Document.Fragment.Menu.Breadcrumb
    languageMenu = Claudio.Render:Document.Fragment.Menu.Language

    mainContent = Claudio.Render:Document.Fragment.Content.Main
    footerContent = Claudio.Render:Document.Fragment.Content.Footer

    bodyClass = ${q(node).parents().count() >= 1 && q(node).children('[instanceof Neos.Neos:Document]').filter('[_hiddenInIndex=false]').count() > 0 ? 'has-subpages' : null}

    renderer = Claudio.Render:Document.Base {
        bodyClass = ${props.bodyClass}
        body = afx`
            <div class="top-navigation-wrap">
                <div class="container">
                    {props.languageMenu}
                    {props.mainMenu}
                    {props.secondaryMenu}
                </div>
            </div>

            <div class="container">
                {props.breadcrumbMenu}
                {props.mainContent}
            </div>

            <footer role="navigation" class="navbar navbar-default">
                <div class="panel panel-default">
                    <div class="panel-body clearfix">
                        <div class="container">
                            {props.metaMenu}
                        </div>
                    </div>
                    <div class="panel-footer">
                        <div class="container">
                            {props.footerContent}
                        </div>
                    </div>
                </div>
            </footer>
        `
    }
}
